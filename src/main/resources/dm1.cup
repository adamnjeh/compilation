package compil;

import java_cup.runtime.*;
import java_cup.runtime.ComplexSymbolFactory.Location;
import compil.ast.*;

parser code {:
  static class Corps {
    AstList<Attribut> attrs = new AstList<>();
    AstList<Methode> meths = new AstList<>();
  }
:}

terminal CLASS, EXTENDS, PUBLIC, RETURN, INT, NEW, NULL;
terminal SYSTEM, OUT, PRINTLN;

terminal LBRACE, RBRACE, LPAREN, RPAREN, SEMI, DOT, COMMA, ASSIGN;

terminal String IDENT;
terminal String INTEGER_LITERAL;

non terminal Axiome axiome;
non terminal AstList<Classe> classeList;
non terminal Classe classe;

non terminal Ident ident;
non terminal Ident parentOpt;

non terminal Corps classeCorps;

non terminal Attribut attribut;
non terminal Methode methode;

non terminal Type type;

non terminal AstList<Parametre> parametres;
non terminal Parametre parametre;

non terminal AstList<Instruction> instructions;
non terminal Instruction instruction;

non terminal Exp expression;

start with axiome;


axiome ::= classeList:cl
         {: Axiome ax = new Axiome(cl);
            ax.addPosition(clxleft, clxright);
            RESULT = ax;
         :}
         ;

classeList ::=
           /* empty */
         {: RESULT = new AstList<Classe>(); :}
         | classeList:cl classe:c
         {: cl.add(c); RESULT = cl; :}
         ;


classe ::= CLASS:kw ident:n parentOpt:p LBRACE:lb classeCorps:cc RBRACE:rb
         {: Classe cl = new Classe(n, p, cc.attrs, cc.meths);
            cl.addPosition(kwxleft, rbxright);
            RESULT = cl;
         :}
         ;

parentOpt ::=
           /* empty */
         {: RESULT = new Ident("Object"); :}
         | EXTENDS:ex ident:p
         {: 
            RESULT = p;
         :}
         ;

classeCorps ::=
            /* empty */
          {: RESULT = new Corps(); :}
          | classeCorps:cc attribut:a
          {: cc.attrs.add(a); RESULT = cc; :}
          | classeCorps:cc methode:m
          {: cc.meths.add(m); RESULT = cc; :}
          ;


attribut ::= type:t ident:i SEMI:se
           {: Attribut at = new Attribut(t, i);
              at.addPosition(txleft, sexright);
              RESULT = at;
           :}
           ;

type ::= INT:tint
       {: Type ty = new Type("int");
          ty.addPosition(tintxleft, tintxright);
          RESULT = ty;
       :}
       | ident:i
       {: Type ty = new Type(i.nom);
          ty.addPosition(ixleft, ixright);
          RESULT = ty;
       :}
       ;


methode ::= PUBLIC:pub type:t ident:n LPAREN:lp parametres:ps RPAREN:rp
            LBRACE:lb instructions:is RETURN:ret expression:e SEMI:se RBRACE:rb
          {: Methode m = new Methode(t, n, ps, is, e);
             m.addPosition(pubxleft, rbxright);
             RESULT = m;
          :}
          ;

parametres ::=
            /* empty */
          {: RESULT = new AstList<Parametre>(); :}
          | parametre:p
          {: AstList<Parametre> l = new AstList<Parametre>(); l.add(p); RESULT = l; :}
          | parametres:ps COMMA:co parametre:p
          {: ps.add(p); RESULT = ps; :}
          ;

parametre ::= type:t ident:i
            {: Parametre p = new Parametre(t, i);
               p.addPosition(txleft, ixright);
               RESULT = p;
            :}
            ;


instructions ::=
              /* empty */
            {: RESULT = new AstList<Instruction>(); :}
            | instructions:is instruction:i
            {: is.add(i); RESULT = is; :}
            ;

instruction ::=
              SYSTEM:sys DOT:d1 OUT:o DOT:d2 PRINTLN:pr LPAREN:lp expression:e RPAREN:rp SEMI:se
            {: InstrPrintln ip = new InstrPrintln(e);
               ip.addPosition(sysxleft, sexright);
               RESULT = ip;
            :}
            | ident:i ASSIGN:as expression:e SEMI:se
            {: InstrAssign ia = new InstrAssign(i, e);
               ia.addPosition(ixleft, sexright);
               RESULT = ia;
            :}
            ;


expression ::=
              INTEGER_LITERAL:s
            {: ExpEntier ee = new ExpEntier(Integer.parseInt(s));
               ee.addPosition(sxleft, sxright);
               RESULT = ee;
            :}
            | ident:i
            {: ExpIdent ei = new ExpIdent(i);
               ei.addPosition(ixleft, ixright);
               RESULT = ei;
            :}
            | NULL:nul
            {: ExpNull en = new ExpNull();
               en.addPosition(nulxleft, nulxright);
               RESULT = en;
            :}
            | NEW:nw ident:i LPAREN:lp RPAREN:rp
            {: ExpNew en = new ExpNew(i);
               en.addPosition(nwxleft, rpxright);
               RESULT = en;
            :}
            ;


ident ::= IDENT:s
        {: Ident id = new Ident(s);
           id.addPosition(sxleft, sxright);
           RESULT = id;
        :}
        ;
